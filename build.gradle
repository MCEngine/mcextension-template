/**
 * Extension Configuration
 */
plugins {
    id 'java'
    id 'maven-publish'
}

// --- DYNAMIC VERSIONING LOGIC ---
// 1. Detect Build Environment
String buildNum = System.getenv("BUILD_NUMBER") // Passed from GitHub Action
String devRelease = System.getenv("DEV_RELEASE_VERSION") // Passed from GitHub Actions
String releaseTag = System.getenv("RELEASE_VERSION") // Passed from GitHub Actions
boolean isDevBuild = (buildNum != null && !buildNum.isEmpty())
boolean isDevRealeaseBuild = (devRelease != null && !devRelease.isEmpty())

// 2. Calculate the Version String IMMEDIATELY
String calculatedVersion = "unspecified"

if (releaseTag != null && !releaseTag.isEmpty()) {
    // RELEASE: yyyy.m.m
    calculatedVersion = releaseTag.replace("v", "")
} else if (project.hasProperty('project-version')) {
    def baseVersion = project.property('project-version')
    def iteration = project.findProperty('project-iteration') ?: "1"

    if (isDevBuild || isDevRealeaseBuild) {
        // CI: yyyy.m.m-build.{number}
        calculatedVersion = "${baseVersion}-build.${buildNum}"
    } else {
        // LOCAL: yyyy.m.m-{iteration}
        calculatedVersion = "${baseVersion}-${iteration}"
    }
}

// 3. Apply it to the project
version = calculatedVersion
group = project.property('project-group')

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/MCEngine/mcextension")
        credentials {
            // Priority: Project Property -> Custom Env Var -> GitHub Default Env Var
            username = project.findProperty("gpr.user") ?: 
                       System.getenv("USER_GITHUB_NAME") ?: 
                       System.getenv("GITHUB_ACTOR")
                       
            password = project.findProperty("gpr.key") ?: 
                       System.getenv("USER_GITHUB_TOKEN") ?: 
                       System.getenv("GITHUB_TOKEN")
        }
    }
    // PaperMC Maven
    maven {
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    // Spigot API often relies on Sonatype for transitive dependencies (like bungeecord-chat)
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    // Adds MCExtension as a compile-time only dependency
    compileOnly 'io.github.mcengine:mcextension:2026.0.3-1-SNAPSHOT'
    // PaperMC API
    compileOnly 'io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT'
}

def gitOwner = project.property('git-org-name')
def gitRepo = project.property('git-org-repository')
def gitUrl = project.property('project-artifact-url')

publishing {
    publications {
        create('mavenJava', MavenPublication) {
            artifactId = project.property('project-artifact-id')
            
            // Standard JAR publication
            from components.java 

            pom {
                name = project.property('project-artifact-name')
                description = project.property('project-artifact-description')
                url = gitUrl

                developers {
                    developer {
                        id = 'jetsadawijit'
                        url = 'https://jetsadawijit.github.io'
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/${gitOwner}/${gitRepo}.git"
                    developerConnection = "scm:git:ssh://github.com/${gitOwner}/${gitRepo}.git"
                    url = gitUrl
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/${gitOwner}/${gitRepo}")

            credentials {
                username = System.getenv("USER_GITHUB_NAME")
                password = System.getenv("USER_GITHUB_TOKEN")
            }
        }
    }
}

